buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.townsfolk:gradle-release:1.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'release'
apply plugin: 'codenarc'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'


group = 'de.felixschulze.gradle'

ext.archivesBaseName = 'gradle-hockeyapp-plugin'
ext.isSnapshot = version.endsWith("-SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.apache.httpcomponents:httpmime:4.5.1'
    compile 'com.android.tools.build:gradle:1.5.0'
    compile 'de.felixschulze.teamcity:teamcity-status-message-helper:1.2'

    testCompile 'junit:junit:4.12'
}

test {
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

codenarcMain {
    exclude "scr/test/**"
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourceJar
}

signing {
    sign configurations.archives
}

// Only *require* signing if we are uploading a non snapshot version
gradle.taskGraph.whenReady { taskGraph ->
    tasks.withType(org.gradle.plugins.signing.Sign).all {
        required = taskGraph.hasTask(":uploadArchives") && !isSnapshot
    }
}

install.repositories.mavenInstaller {
    pom.project(pomConfiguration)
    if (signing.signatory) {
        beforeDeployment { signing.signPom(it) }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        if (signing.signatory) {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        }
        name = 'mavenCentralReleaseDeployer'
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'always')
        }
        pom.project(pomConfiguration)
    }
}

release {
    createReleaseTag.dependsOn uploadArchives
}

codenarc {
    ignoreFailures = true
}

/**v
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    return {
        name 'Gradle HockeyApp Plugin'
        packaging 'jar'
        description 'A Gradle plugin for uploading iOS and Android Apps to HockeyApp.'
        url 'https://github.com/x2on/gradle-hockeyapp-plugin'
        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'x2on'
                name 'Felix Schulze'
                email 'code@felixschulze.de'
            }
        }
        scm {
            connection 'scm:https://x2on@github.com/x2on/gradle-hockeyapp-plugin'
            developerConnection 'scm:git@github.com:x2on/gradle-hockeyapp-plugin.git'
            url 'https://github.com/x2on/gradle-hockeyapp-plugin'
        }
    }
}